import com.redpillanalytics.aws.tasks.S3UploadSyncTask
import com.redpillanalytics.aws.tasks.S3DownloadSyncTask

plugins {
   id "com.gradle.plugin-publish" version "0.10.1"
   id "pl.allegro.tech.build.axion-release" version "1.10.1"
   id "com.github.breadmoirai.github-release" version "2.2.8"
   id 'groovy'
   id 'java-gradle-plugin'
   id 'org.unbroken-dome.test-sets' version '2.1.1'
   //id "com.github.johnrengelman.shadow" version "4.0.3"
   id "com.github.ben-manes.versions" version "0.21.0"
   id 'com.adarshr.test-logger' version '1.6.0'
   id "com.redpillanalytics.gradle-analytics" version "1.2.3"
   id "com.avast.gradle.docker-compose" version "0.9.4"
   id "com.redpillanalytics.gradle-aws" version "0.1.2"
}

// send analytics
analytics {
   organization = 'Red Pill Analytics'
   sinks {
      pubsub
      s3 {
         prefix = 'rpa-gradle-analytics'
      }
   }
}

scmVersion {

   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = true
}

//set Gradle version to SCM Version
allprojects {
   project.version = scmVersion.version
}

// Default artifact naming.
group = 'com.redpillanalytics'
ext.pluginName = 'checkmate.odi'

// Build ODI container
ext.sdkBucket = 'rpa-odi-sdk'
ext.odiVersion = '12.2.1.3.0'
ext.odiSdkHome = "odi-sdk"

// Download ODI SDK
task copyOdiSdk(type: S3DownloadSyncTask) {
   description 'Copy the ODI SDK JAR files from S3.'
   group 'build'
   bucketName sdkBucket
   keyName odiVersion
   filePath odiSdkHome
}

tasks.compileGroovy.dependsOn tasks.copyOdiSdk
tasks.compileJava.dependsOn tasks.copyOdiSdk

gradlePlugin {
   plugins {
      checkmateOdi {
         id = "com.redpillanalytics.${pluginName}"
         implementationClass = 'com.redpillanalytics.odi.gradle.OdiPlugin'
      }
   }
}

pluginBundle {

   description = "A CI/CD framework for Oracle Data Integrator."
   website = "https://github.com/RedPillAnalytics/${rootProject.name}/"
   vcsUrl = "https://github.com/RedPillAnalytics/${rootProject.name}/"

   plugins {

      checkmateOdi {
         displayName = pluginName
         tags = ['oracle', 'data-integration', 'etl']
         version = project.version
      }
   }
}

githubRelease {

   token = githubToken
   owner = 'RedPillAnalytics'
   repo = rootProject.name
   tagName = "v${version}"
   releaseAssets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
}

dependencies {

   compile gradleApi()
   compile localGroovy()

   compile group: 'org.slf4j', name: 'slf4j-simple', version: '+'
   compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '+'

   // Gradle Analytics plugin
   // It also has the Common library in it, thus the dependency here
   compile "gradle.plugin.com.redpillanalytics:gradle-analytics:+"

   // ODI SDK copied down from S3
   compile fileTree(dir: odiSdkHome, include: '*.jar')

   testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'

   // Plugin so classes can be used
   compile "gradle.plugin.com.redpillanalytics:gradle-aws:0.1.2"
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
   maven {
      url releaseUrl
      authentication {
         awsIm(AwsImAuthentication)
      }
   }
   maven {
      url snapshotUrl
      authentication {
         awsIm(AwsImAuthentication)
      }
   }
}

// Options for all tests
tasks.withType(Test) {
   testLogging.showStandardStreams true
   systemProperty 'projectDir', temporaryDir
   systemProperty 'odiPassword', odiPassword
   systemProperty 'masterPassword', masterPassword
   systemProperty 'masterUrl', masterUrl
}

testSets {
   onlineExportTest
   onlineImportTest
}

//customize ShadowJar
//jar.enabled = false

//shadowJar {
//   classifier = 'all'
//   zip64 = true
//}

//tasks.build.dependsOn tasks.shadowJar

task('cleanJunit', type: Delete) {
   delete getTestResultsDir()
}

onlineExportTest.mustRunAfter onlineImportTest

ext.docsBucket = 'documentation.redpillanalytics.com'
ext.resourceBucket = 'rpa-build-resources'

task publishOdiSdk(type: S3UploadSyncTask) {
   description 'Publish the ODI SDK JAR files to S3.'
   group 'publish'
   bucketName sdkBucket
   keyName odiVersion
   logger.warn "filepath: ${odiSdkHome}/${odiVersion}"
   filePath "${odiSdkHome}/${odiVersion}"
}

task publishVersionDocs(type: S3UploadSyncTask) {

   description = 'Upload version Groovydocs to s3.'
   group = 'documentation'

   bucketName docsBucket
   filePath groovydoc.destinationDir.getPath()
   keyName "${rootProject.name}/${version}"

   dependsOn groovydoc
}

task publishLatestDocs(type: S3UploadSyncTask) {

   description = 'Upload latest Groovydocs to s3.'
   group = 'documentation'

   bucketName docsBucket
   filePath groovydoc.destinationDir.getPath()
   keyName "${rootProject.name}/latest"

   dependsOn groovydoc
}

task copyBuildResources(type: S3DownloadSyncTask) {
   description 'Copy build resource files from S3.'
   group 'build'
   bucketName resourceBucket
   keyName 'gradle'
}

task runAllTests {
   description = 'Run all defined tests.'
   group = 'verification'
   dependsOn onlineExportTest, onlineImportTest
}

//publishing {
//   repositories {
//      maven {
//         url project.version.endsWith('-SNAPSHOT') ? snapshotUrl : releaseUrl
//         authentication {
//            awsIm(AwsImAuthentication)
//         }
//      }
//   }
//}